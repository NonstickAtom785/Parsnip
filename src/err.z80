err_bad_index:
  ld a,14
  jr Err

err_unknown:
err_bad_name:
  ld a,13
  jr Err

err_label_not_found:
  ld a,12
  jr Err

err_heap_overflow:
  ld a,11
  jr Err

err_operand_stack_overflow:
err_operand_stack_overflow_pop:
  ld a,10
  jr Err

err_too_many_args:
err_too_many_params:
  ld a,9
  jr Err

err_too_few_args:
err_missing_parameter:
  ld a,8
  jr Err
err_could_not_convert:
err_bad_type:
  ld a,7
  jr Err
err_syntax:
  ld a,6
  jr Err
err_stack_overflow_pop:
  ld a,5
  jr Err
err_stack_overflow_push:
  ld a,4
  jr Err
ErrBadToken:
  ld a,3
  jr Err
err_ON:
  ld a,2
  jr Err
err_mem:
  ld a,1
  .db $FE
err_fatal:
err_internal_fatal:
  xor a
Err:
  ld hl,error_LUT
  add a,a
  cp error_LUT_end-error_LUT
  jr c,+_
  xor a
_:
  add a,l
  ld l,a
  jr nc,$+3
  inc h
  ld a,(hl)
  inc hl
  ld h,(hl)
  ld l,a

CustomError:
  ld de,appErr1
  call mov13
  ld a,43
  .db $FE  ;start of cp *
StopToken:
BreakProgram:
  xor a
  push af

;Reset the keypress
_:
  call GetKeyDebounce
  or a
  jr nz,-_

;Reset the link port
  out (0),a

;Reset the OS keypress
  ld (843Fh),a
  ld (8444h),a

;Reset textShadow and cmdShadow
	bcall(_clrTxtShd)
  bcall(_saveCmdShadow)

;destroy the heap to save space
  call heap_destroy

  pop af
  ld hl,(parsePtr)
  dec hl
  ld (parsePtr),hl
  ld sp,(sp_save)
  di
  im 1
  or a
  jr z,+_
  ld b,a
  ld a,(basic_prog)
  cp 5
  ld a,b
  jr nz,$+4
  or 128
  bcall(_JError)
_:
  or 1
  ret


error_LUT:
  .dw s_err_fatal
  .dw s_err_mem
  .dw s_err_ON
  .dw s_err_BadToken
  .dw s_err_stack_overflow_push
  .dw s_err_stack_overflow_pop
  .dw s_err_syntax
  .dw s_err_bad_type
  .dw s_err_missing_args
  .dw s_err_too_many_args
  .dw s_err_stackoverflow_operands
  .dw s_err_heap_overflow
  .dw s_err_label_not_found
  .dw s_err_bad_name
  .dw s_err_bad_index
  ; .dw s_err_
  ; .dw s_err_
  ; .dw s_err_
error_LUT_end:





s_err_fatal:
  .db "Fatal",0
s_err_mem:
  .db "Mem",0
s_err_ON:
  .db "Break",0
s_err_BadToken:
  .db "Bad Token",0
s_err_stack_overflow_push:
s_err_stack_overflow_pop:
s_err_stackoverflow_operands:
  .db "StackOverflw",0
s_err_syntax:
  .db "Syntax",0
s_err_bad_type:
  .db "Bad Type",0
s_err_missing_args:
  .db "Missing Args",0
s_err_too_many_args:
  .db "TooManyArgs",0
s_err_heap_overflow:
  .db "HeapOverflow",0
s_err_label_not_found:
  .db "LblNotFound",0
s_err_bad_name:
  .db "Bad Name",0
s_err_bad_index:
  .db "Bad Index",0
