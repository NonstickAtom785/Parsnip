SetUpData:
  bcall(_RunIndicOff)
  call setupRAMdata
  ld hl,ReadArcData
  ld de,TSA
;copy 71 bytes
  call mov14
  call mov14
  call mov14
  call mov14
  call mov14
  ldi
  in a,(6)
  ld (page_restore),a

  ld hl,plotSScreen
  ld (buf_ptr),hl
  ld (graybuf_ptr),hl

;Set up operand and operator stack pointers
  ld hl,appBackUpScreen
  ld (operandstack_ptr),hl
  ld (operandstack_bottom),hl
  inc h
  inc h
  ld (operandstack_top),hl
  xor a
  inc h
  dec hl
;  ld hl,appBackUpScreen+767
  ld (opstack_ptr),hl
  ld (hl),a

;Set up values for variable names
  ld h,a
  ld l,a
  ld (vars_count),hl
  ld hl,saveSScreen
  ld (var_names_base),hl
  ld (var_names_ptr),hl
  ld hl,saveSScreen+768
  ld (var_names_top),hl

; Need to clear 108 bytes at pvars
  ld b,108
  ld hl,pvars
  call SetSmallMem

;
  ld h,a
  ld l,a
  ld (flags+33),a
  ld (flags+34),hl

  ld a,3
  ld (text_logic),a
  ld (text_delay),a
  ld hl,FontSet
  ld (font_ptr),hl

  ld hl,$0155
  ld (gray_mask),hl

  pop hl
  ld (sp_save),sp
  jp (hl)

setupRAMdata:
;not speed critical
  ld de,8100h
  ld hl,RAMCodeStart
  ld bc,RAMCodeEnd-RAMCodeStart
  ldir
  ret

heap_init:
;Set up heap pointers
  call create_heap
  ld (heap_ptr),hl
  ld (heap_bottom),hl
  add hl,bc
  ld (heap_top),hl
  ret

heap_destroy:
  ld hl,heapvar_name
  rst rMov9ToOP1
  bcall(_ChkFindSym)
  bcall(_DelVarArc)
  ld hl,0
  ld (heap_ptr),hl
  ld (heap_bottom),hl
  ld (heap_top),hl
  ret

create_heap:
  ld hl,heapvar_name
  rst rMov9ToOP1
  bcall(_ChkFindSym)
  jr nc,+_
  ;It doesn't exist, so let's create it.
  bcall(_MemChk)
  ld bc,-20
  add hl,bc
  jp z,err_mem
  jp nc,err_mem
  ld a,22     ;temp program
  bcall(_CreateVar)
_:
  ex de,hl
  ld c,(hl)
  inc hl
  ld b,(hl)
  inc hl
  ret

heapvar_name:
  .db 22,"heap",0
