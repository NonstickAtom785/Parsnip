;chardim:
;Get the height and width of a char in the current font.
;Input: A is the char
;Output: B is the width, C is height
;Destroys: A
  ld b,a
  ld a,(text_mode)
  dec a
  jp z,var_width_size
  ld bc,$0608
  cp 2
  ret z
  ld bc,$0406
  ret
var_width_size:
  push hl
  push de
  ld hl,(font_ptr)
  ld d,a
  .db $C2  ;start of `jp nz,*`
_:
  add a,2
  ld e,a
  add hl,de
  ld a,(hl)
  ;a is w,h in nibbles
  and 15
  djnz -_
  ld c,a
  xor (hl)
  rrca
  rrca
  rrca
  rrca
  ld b,a
  pop de
  pop hl
  ret
