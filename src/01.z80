.org $4000
#define scrap 8000h

#include "xconst.z80"
#include "float.inc"
#include "single.inc"
#include "strtox.z80"
#include "xtostr.z80"
#include "xpow.z80"
#include "xexp.z80"
#include "xcmp.z80"
#include "xneg.z80"
#include "xrand.z80"
#include "xinv.z80"


#include "absSingle.z80"
#include "negSingle.z80"
#include "cmpSingle.z80"

#include "randSingle.z80"

#include "acosSingle.z80"
#include "asinSingle.z80"
#include "atanSingle.z80"
#include "acoshSingle.z80"
#include "asinhSingle.z80"
#include "atanhSingle.z80"
#include "coshSingle.z80"
#include "sinhSingle.z80"
#include "tanhSingle.z80"
#include "tanSingle.z80"


#include "powSingle.z80"
#include "pow10Single.z80"
#include "logSingle.z80"
#include "lgSingle.z80"
#include "log10Single.z80"

#include "single2str.z80"
#include "str2single.z80"
#include "singleTo_int16.z80"

#include "math/multiplication/mulfixed1616.z80"

xconst_to_xOP1:
  call xconst
  ld de,xOP1
  jp mov10

xconst_to:
  call xconst
  jp mov10

pop_operand_p1:
  ld hl,pop_operand
  ld (prev_page_call_address),hl
  jp prev_page_call

popoperand_ui8_p1:
  ld hl,popoperand_ui8
  ld (prev_page_call_address),hl
  jp prev_page_call

popoperand_ui16_p1:
  ld hl,popoperand_ui16
  ld (prev_page_call_address),hl
  jp prev_page_call

pop_end_of_params_p1:
  ld hl,pop_end_of_params
  ld (prev_page_call_address),hl
  jp prev_page_call

pushoperand_ui16_p1:
  ld hl,pushoperand_ui16
  ld (prev_page_call_address),hl
  jp prev_page_call

pushoperand_char_p1:
  ld hl,pushoperand_char
  ld (prev_page_call_address),hl
  jp prev_page_call

err_bad_input_p1:
  ld hl,err_bad_input
  ld (prev_page_call_address),hl
  jp prev_page_call

err_syntax_p1:
  ld hl,err_syntax
  ld (prev_page_call_address),hl
  jp prev_page_call

err_fatal_p1:
  ld hl,err_fatal
  ld (prev_page_call_address),hl
  jp prev_page_call

eval_func_p1:
;A is the func to evaluate, <128
  ld hl,internal_eval_table
  add a,a
  add a,l
  ld l,a
  jr nc,$+3
  inc h
  ld a,(hl)
  inc hl
  ld h,(hl)
  ld l,a
  jp (hl)
internal_eval_table:
  .dw eval_getmode
  .dw eval_setmode
  .dw eval_char
  .dw eval_setfont
  .dw eval_method

eval_setfont:
  call popoperand_ui8_p1
  ld (text_mode),a
  ld hl,FontSet
  dec a
  jr nz,+_
  ld hl,vFont
_:
  ld (font_ptr),hl
  jp pop_end_of_params_p1

eval_setmode:
  call popoperand_ui16_p1
  ld (flags+userflags),de
  jp pop_end_of_params_p1

eval_char:
  call popoperand_ui8_p1
  push af
  call pop_end_of_params_p1
  pop af
  jp pushoperand_char_p1


eval_getmode:
  call pop_end_of_params_p1
  ld de,(flags+userflags)
  jp pushoperand_ui16_p1

eval_method:
;opstack should contain a 0, then the type, then the function number*2
  ld hl,(opstack_ptr)
  ld a,(hl)
  or a
  jp nz,err_fatal_p1
  inc hl
  ld a,(hl)
  inc hl
  ld c,(hl)
  inc hl
  ld (opstack_ptr),hl
  ld hl,eval_method_type_LUT
  add a,a
  add a,l
  ld l,a
  jr nc,$+3
  inc h
  ld a,(hl)
  inc hl
  ld h,(hl)
  ld l,a
  or h
  jp z,err_syntax_p1
  ;now HL points to the table for this type
  ld b,0
  add hl,bc   ;BC is already times 2
  ld a,(hl)
  inc hl
  ld h,(hl)
  ld l,a
  jp (hl)

eval_method_type_LUT:
  .dw 0               ;00 ui8
  .dw 0               ;01
  .dw 0               ;02
  .dw 0               ;03
  .dw 0               ;04
  .dw 0               ;05
  .dw 0               ;06
  .dw 0               ;07
  .dw 0               ;08
  .dw 0               ;09
  .dw 0               ;0A
  .dw 0               ;0B
  .dw 0               ;0C
  .dw 0               ;0D
  .dw 0               ;0E
  .dw 0               ;0F
  .dw 0               ;10
  .dw 0               ;11
  .dw 0               ;12
  .dw method_display  ;13 display

method_display:
  .dw display_contrast
  .dw display_flip
  .dw display_zshift
  .dw display_off
  .dw display_on

display_contrast:
  call popoperand_ui8_p1
  add a,$C0
  jp c,err_bad_input_p1
  out (16),a
  call pop_end_of_params_p1
  jp pop_operand_p1

display_zshift:
  call popoperand_ui8_p1
  and 63
  add a,$40
  out (16),a
  call pop_end_of_params_p1
  jp pop_operand_p1

display_off:
  ld a,3
  out (16),a
  call pop_end_of_params_p1
  jp pop_operand_p1

display_on:
  ld a,2
  out (16),a
  call pop_end_of_params_p1
  jp pop_operand_p1

display_flip:
;Some calcs have an LCD with commands to flip the display vertically/horizontally
  call popoperand_ui8_p1
  and 3
  add a,$0C
  out (16),a
  call pop_end_of_params_p1
  jp pop_operand_p1

.echo "Page 1: ",$8000-$," bytes remaining"
