z32_0 = xOP3+32

eval_sqr_LUT:
  .db (+_-$-1)/2
  .dw err_syntax      ;should not have run out of args!
  .dw sqr_ui8
  .dw sqr_ui16
  .dw sqr_ui32
  .dw sqr_fixed88
  .dw sqr_fixed1616
  .dw sqr_var
  .dw sqr_true
  .dw sqr_false
  .dw sqr_tstr_ref
  .dw sqr_raw
  .dw sqr_str
  .dw sqr_str_ref
  .dw sqr_single
  .dw sqr_xfloat
_:


sqr_true:
sqr_false:
;1*1 = 1, 0*0=0
;nice, just exit
  ret

sqr_ui32:
  call popoperand_ui32
  ld bc,mul32
  ld (next_page_call_address),bc
  push de
  pop bc
  push hl
  pop ix
  call next_page_call
  ;z0_32 has the result
  ld hl,(z32_0)
  ld de,(z32_0+2)
  ld a,d
  or e
  jp nz,pushoperand_ui32
  jp pushoperand_ui16_HL


sqr_fixed88:
  call popoperand_fixed88
  ld b,d
  ld c,e
  call mulfixed_88
  ex de,hl
  jp pushoperand_fixed88


sqr_fixed1616:
  call popoperand_fixed1616
  push de
  pop bc
  push hl
  pop ix
  call mulfixed16_16_p0
  jp pushoperand_fixed1616

;
sqr_var:
sqr_tstr_ref:
sqr_raw:
sqr_str:
sqr_str_ref:
  jp err_syntax

sqr_ui8:
sqr_ui16:
  call popoperand_ui16
  ld b,d
  ld c,e
  call DE_Times_BC
  ld a,d
  or e
  jp nz,pushoperand_ui32
  jp pushoperand_ui16_HL

sqr_single:
  call popoperand_single
  ld d,h
  ld e,l
  ld bc,(operandstack_ptr)
  call mulSingle_p0
;WARNING: This routine assumes there is enough space on the operand stack!
  ld hl,4
  add hl,bc
  ld (hl),type_single
  inc hl
  ld (operandstack_ptr),hl
  ret

sqr_xfloat:
  call popoperand_xfloat
  ld d,h
  ld e,l
  ld bc,(operandstack_ptr)
  call xmul_p0
  jp push_special_xfloat
